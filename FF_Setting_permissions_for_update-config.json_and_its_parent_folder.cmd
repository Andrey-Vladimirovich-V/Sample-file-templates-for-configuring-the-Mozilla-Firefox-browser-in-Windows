@Echo Off
::@Echo On

:: Выставляем кодовую страницу "Кириллица OEM 866".
CHCP 866

@Echo.
@Echo Назначение данного CMD-файла.
@Echo.
@Echo Замена файла update-config.json и настройка безопасности для него и его родительской папки
@Echo таким образом, чтобы пользователи не могли менять его содержимое. Данные изменения
@Echo затрагивают только браузер FireFox.
@Echo.
@Echo.

:: Включение расширенной обработки команд (EnableExtensions)
:: и отложенного раскрытия переменных среды (EnableDelayedExpansion)
SetLocal EnableExtensions
:: EnableDelayedExpansion буду включать по необходимости,
:: т.к. иначе восклицательные знаки в значениях переменных пропадают.
:: Включение - SetLocal EnableDelayedExpansion .
:: Выключение - EndLocal или SetLocal DisableDelayedExpansion .

:: Для нормального отображения русских слов при редактировании файла и его работе,
:: кодировка должна быть Кириллица OEM 866.
:: 

::@Echo.
::@Echo Раскладка клавиатуры должна быть английской!
::@Echo.
::@Echo Keyboard layout has to be English!
::@Echo.

@Echo.
@Echo ФАЙЛ НУЖНО ЗАПУСКАТЬ С ПРАВАМИ АДМИНИСТРАТОРА!
@Echo.

@Echo.
@Echo На момент запуска данного CMD-файла браузер FireFox 
@Echo уже должен быть установлен в ОС и хотя бы один раз запущен!
@Echo.

@Echo.
@Echo Последовательность действий.
@Echo.
@Echo 01. Создание группы пользователей которой будет запрещено изменять файл "update-config.json".
@Echo 02. Наполнение созданной группы пользователями (по желанию запустившего данный CMD-файл).
@Echo 03. Смена владельца на группу "Администраторы" для директории FireFox'а
@Echo "%ProgramData%\Mozilla-????????-????-????-????-????????????\updates\????????????????" и 
@Echo всего её содержимого.
@Echo 04. Сброс прав для указанной выше директории и всего её содержимого. После этого права у 
@Echo указанной директориии и всего её содержимого будут такие же, как у её родительской директории.
@Echo 05. Копирование подготовленного файла "update-config.json" для FireFox'а в директорию FireFox'а
@Echo "%ProgramData%\Mozilla-????????-????-????-????-????????????\updates\????????????????\".
@Echo 06. Настройка прав для файла "update-config.json" и его родительской папки таким образом,
@Echo чтобы нельзя было удалять этот файла и менять его содержимое указанной группе пользователей.

@Echo.
@Echo Далее, для подстраховки, будет две статичных паузы. 
@Echo Если хотите прервать выполнение данного CMD-файла
@Echo закройте консольное окно или нажмите комбинацию кнопок 
@Echo "Ctrl" + "C" и согласитесь на завершение
@Echo выполнения пакетного файла.
@Echo.

Pause
Pause

:: 000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000
:: 000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000
:: 000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000
:: 000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000

:: В общем, пока пришёл к следующему варианту.
:: 
:: Моя задача установить в принудительном порядке следующие настройки обновления:
:: "Автоматически устанавливать обновления (желательно)" ("Automatically install updates (recommended)") - да.
:: "Когда Firefox не запущен" ("When Firefox is not running") - нет.
:: "Использовать фоновую службу для установки обновлений" ("Use a background service to install updates") - да.

:: Последний параметр без проблем устанавливается через файл "firefox.cfg" (поправка, в FF 128.4.0 ESR x64 
:: этот параметр включён, заблокирован и убран из настроек через визуальный интерфейс), поэтому в файле 
:: "update-config.json" я оперирую только первыми двумя (правда, я не знаю, можно ли тут ещё 
:: что-либо менять).
:: 
:: В файл "update-config.json" записываю строку (без пробела и точки в конце. Тут их я поставил только 
:: для того, чтобы обозначить, что дальше записи нет):
:: {"__DEFAULTS__":{"app.update.auto":true,"app.update.background.enabled":false}} .
:: По умолчанию этот файл имеет только строку:
:: {"__DEFAULTS__":{"app.update.auto":true,"app.update.background.enabled":true}} .
:: Если оба параметра будут выключены через GUI, строка примет вид:
:: {"__DEFAULTS__":{"app.update.auto":true,"app.update.background.enabled":true},"app.update.background.enabled":false,"app.update.auto":false} .
:: 
:: Исходя из названий тут может быть некоторая путаница, но, проверка показала, что 
:: "app.update.background.enabled" действительно имеет отношение к параметру "Когда Firefox не запущен" 
:: ("When Firefox is not running").
:: 
:: Сам браузер в Win использует в файле "update-config.json" кодировку UTF-8 и последовательность конца 
:: строки Win (CR LF). Но, для подстраховки я использую последовательность конца строки Unix (LF).
:: 
:: Будем настраивать безопасность файла и директории в которой он находится так, чтобы нельзя было менять и
:: удалять этот файл. Правильно было бы настраивать и две другие родительские директории, но пока лень
:: это делать. Может, потом сделаю.
:: 
:: Изначально, имеются следующие права:
:: 
:: Для директории "%ProgramData%\Mozilla-????????-????-????-????-????????????\updates\????????????????".
:: Система - Разрешить - Для этой папки, её подпапок и файлов - Полный доступ.
:: Администраторы - Разрешить - Для этой папки, её подпапок и файлов - Полный доступ.
:: Пользователи - Разрешить - Для этой папки, её подпапок и файлов - Полный доступ.
:: Владелец - имя пользователя от которого запускался браузер при создании этого объекта. Как правило,
:: это наша учётная запись пользователя.
:: 
:: Для файла "%ProgramData%\Mozilla-????????-????-????-????-????????????\updates\
:: ????????????????\update-config.json".
:: Система - Разрешить - Полный доступ.
:: Администраторы - Разрешить - Полный доступ.
:: Пользователи - Разрешить - Полный доступ.
:: Владелец - имя пользователя от которого запускался браузер при создании этого объекта. Как правило,
:: это наша учётная запись пользователя.
:: 
:: Сначала нужно настроить файл, а потом уже директорию (для смены прав требуются административные права).
:: Имена директорий мы должны определить самостоятельно, но такое впечатление, что они стандартные, т.к.
:: идентичны у меня на железном и виртуальном ПК. Хотя, я был уверен, что имена генерируются с 
:: использованием случайных чисел. А иначе, зачем они такие? Эти имена прописаны в данном CMD-файле. Если 
:: они изменятся, нужно будет их тут поменять.
:: Изначально владельцем файла и директории может быть наша учётная запись, а это потенцильно даёт
:: серьёзные права. Для подстраховки я делаю владельцем систему.
:: Для прописывания запретов я использую группу, в которую помещаю учётные записи пользоавтелей. 
:: Так удобнее, т.к. не нужно при смене пользователей менять настройки прав. Казалось бы, можно 
:: просто запретить для папки менять её содержимое. Но, в ней же находятся и другие объекты, а в 
:: будущем, могут появится и новые. Нельзя запрещать их менять.
:: 
:: 
:: После всех манипуляций права должны быть следующие:
:: 
:: Для директории "%ProgramData%\Mozilla-????????-????-????-????-????????????\updates\????????????????".
:: Система - Разрешить - Для этой папки, её подпапок и файлов - Полный доступ.
:: Администраторы - Разрешить - Для этой папки, её подпапок и файлов - Полный доступ.
:: Пользователи - Разрешить - Для этой папки, её подпапок и файлов - Полный доступ.
:: "Имя_нашей_группы" - Запретить - Только для этой папки - Удаление подпапок и файлов, Удаление, 
:: Смена разрешений, Смена владельца.
:: Владелец - Система.
:: 
:: Для файла "%ProgramData%\Mozilla-????????-????-????-????-????????????\updates\
:: ????????????????\update-config.json".
:: Система - Разрешить - Полный доступ.
:: Администраторы - Разрешить - Полный доступ.
:: Пользователи - Разрешить - Чтение и выполнение.
:: "Имя_нашей_группы" - Запретить - Создание файлов / Запись данных, Создание папок / Дозапись данных,
:: Удаление, Смена разрешений, Смена владельца.
:: Владелец - Система.



:: На случай, если вручную будем менять права и напортачим с настройками безопасности NTFS, 
:: есть возможность вернуть всё обратно. 
:: Для этого сначала принудительно делаем владельцем всего содержимого папки 
:: "%ProgramData%\Mozilla-????????-????-????-????-????????????\updates\????????????????" группу 
:: "Администраторы" при помощи программы "TakeOwn.exe", а потом сбрасываем права для этой же папки и 
:: всего её содержимого при помощи той же программы, что и настраивали права.
:: Код.
:: Делаем владельцем группу "Администраторы" для этой папки и всего её содержимого.
::"TakeOwn.exe" /F "%Full_Dir_name%" /A /R /D Y
:: Сбрасываем права.
::"ICACLS.exe" "%Full_Dir_name%" /reset /T /C
:: 
:: Все эти действия можно проделать при помощи файла "FF_Reset_permissions_for_directory_and_all_its_contents.cmd".
:: 
:: 
:: Для изменения файла "update-config.json" после наших настроек можно воспользоваться следующими способами:
:: 
:: 1. Самый правильный вариант. Повторно воспользоваться данным CMD-файлом 
:: "FF_Setting_permissions_for_update-config.json_and_its_parent_folder.cmd" предварительно изменив 
:: файл "update-config.json" и скопировав его в директорию с указанным CMD-файлом.
:: 
:: 2. Использовать ограниченную учётную запись пользователя и таким образом администраторы компьютера 
:: не будут подвержены этим настройкам. Мы просто все манипулияции с файлом "update-config.json" будем 
:: производить от имени административной учётной записи. После манипуляций с файлом "update-config.json" 
:: может потребоваться настройка его безопасности.
:: 
:: 3. Сбросить права с помощью "TakeOwn.exe" и далее "ICACLS.exe". Но потом нужно будет их настроить заново.
:: 
:: 4. Убрать из используемой нами группы пользователя, от имени которого будут производится измнения
:: в файле "update-config.json" (через оснастку "Локальные пользователи и группы" ("Local Users and 
:: Groups") "LUsrMgr.msc" или командой ( "Net.exe" LocalGroup "Имя_нашей_группы" "Имя_нашего_пользователя" 
:: /Delete )), перезайти в ОС этой учётной записью или перезагрузить ОС. Обновление групповой политики 
:: (запуск файла "GPUpdate.exe") почему-то не помогает, хотя мне казалось, что должено было.
:: Но, после этих манипуляций, возможно, потребуется заново настроить безопасность файла 
:: "update-config.json". Так же нужно будет вернуть пользователя в группу.
:: 
:: 5. Использовать дополнительную административную учётную запись пользователя, которая не прописана в 
:: используемой нами группе пользователей и все манипуляции с файлом "update-config.json" производить 
:: с её правами. Тут тоже после манипуляций с файлом "update-config.json" может потребоваться настройка 
:: его безопасности.

:: 000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000
:: 000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000
:: 000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000
:: 000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000

:: Этот код должен быть в самом начале, т.к. может понадобиться до определения других переменных.
:: Объяснение использования переменной вместо набора знаков.
:: Есть у меня подозрение, что вот такой код "Set "File_7z=%~dp0%Folder_7-Zip%\7za.exe"" иногда 
:: почему-то не работает. Поэтому, вместо использования набора знаков "%~dp0" далее в коде буду 
:: использовать переменную. Скорее всего, во всех других случаях это не нужно, но раз уж создаём 
:: переменную, имеет смысл использовать её везде.
:: 
:: Получаем полный путь до папки, в которой находится данный CMD-файл. Нужно учитывать, 
:: что в конце обязательно будет слэш. Далее, мы его удалим. 
:: F_P_t_S - Full path to script.
Set "F_P_t_S=%~dp0"
:: 
:: Если последним символом в пути будет слэш, то удаляем его. Кстати, например, ICACLS.exe 
:: отказывается работать с папкой в конце пути которой слэш.
If /I "%F_P_t_S:~-1%"=="\" Set "F_P_t_S=%F_P_t_S:~0,-1%"
:: 
:: На случай катастрофических ошибок в коде выбираем текущей директорией ту,
:: в которой находится данный CMD-файл. Для того, чтобы не наломать дров в папке, 
:: назначенной текущей, по умолчанию.
CD /D "%F_P_t_S%"

:: Использованные встроенные участники безопасности и их идентификаторы.
:: 
:: Система - S-1-5-18.
:: Группа "Администраторы" - S-1-5-32-544.
:: Группа "Пользователи" - S-1-5-32-545.


:: Переменные для укорачивания строки кода (т.к. я предпочитаю указывать полные пути).
:: 
:: Системная программа для управления пользователями,
:: группами, службами и сетевыми подключениями.
Set "N_e_t=%SystemRoot%\System32\Net.exe"
:: Системная программа для настройки прав директорий и файлов.
Set "ICACLS_=%SystemRoot%\System32\ICACLS.exe"
:: Системная программа для восстанавления владельца директории или файла.
Set "Take_Own=%SystemRoot%\System32\TakeOwn.exe"


:: Определяем нужный нам путь.
:: 
:: На сколько я смог понять имена этих дирректорий не меняются, хоть и выглядят они как случайно 
:: сгенерированные. Если они изменятся, придётся здесь их корректировать. Полный путь будет вида 
:: "C:\ProgramData\Mozilla-????????-????-????-????-????????????\updates\????????????????".
:: 
:: В реестре есть такая запись, но, можно ли ей верить в долгосрочной перспективе, это вопрос.
:: [HKEY_LOCAL_MACHINE\SOFTWARE\Mozilla\Firefox\TaskBarIDs]
:: [HKEY_LOCAL_MACHINE\SOFTWARE\WOW6432Node\Mozilla\Firefox\TaskBarIDs]
:: "C:\\Program Files\\Mozilla Firefox"="308046B0AF4A39CB"
:: REG_SZ
:: Совпадает с директорией - 
:: "C:\ProgramData\Mozilla-1de4eec8-1241-4177-a864-e594e8d1fb38\updates\308046B0AF4A39CB"
:: 
Set "Mozilla_Dir_name=Mozilla-1de4eec8-1241-4177-a864-e594e8d1fb38"
Set "FF_Dir_name=308046B0AF4A39CB"
Set "Full_Dir_name=%ProgramData%\%Mozilla_Dir_name%\updates\%FF_Dir_name%"


::@Echo.
::@Echo Введите или вставьте имя группы пользователей и нажмите клавишу ввода (если такой группы нет, она будет создана): 
@Echo.
Set /P Group_name="Введите или вставьте имя группы пользователей и нажмите клавишу ввода (если такой группы нет, она будет создана): "

:: Определяем значение переменной на случай отсутствия ответа пользователя на вопрос.
Set "Enter_group_description=No"

::@Echo.
::@Echo Будете вводить описание группы? Если да, то введите ( Y ) или ( y ) и нажмите клавишу ввода. Если нет, то нажмите клавишу ввода без ввода данных или введите что хотите. 
@Echo.
Set /P Enter_group_description="Будете вводить описание группы? Если да, то введите ( Y ) или ( y ) и нажмите клавишу ввода. Если нет, то нажмите клавишу ввода без ввода данных или введите что хотите: "

If /I Not "%Enter_group_description%"=="Y" Goto Skip_entering_group_description

:: Определяем значение переменной на случай отсутствия ответа пользователя на вопрос.
Set "Group_Description=Нет описания."

::@Echo.
::@Echo Введите или вставьте описание создаваемой группы и нажмите клавишу ввода: 
@Echo.
Set /P Group_Description="Введите или вставьте описание создаваемой группы и нажмите клавишу ввода: "

:Skip_entering_group_description

@Echo Создаём группу.
If /I "%Enter_group_description%"=="Y" "%N_e_t%" LocalGroup "%Group_name%" /Add /Comment:"%Group_Description%"
If /I Not "%Enter_group_description%"=="Y" "%N_e_t%" LocalGroup "%Group_name%" /Add
:: Если группа уже есть, но мы хотим создать или поменять её описание, то используем команду
If /I "%Enter_group_description%"=="Y" "%N_e_t%" LocalGroup "%Group_name%" /Comment:"%Group_Description%"
:: (иначе, при попытке создания группы, комментарий будет проигнорирован, т.к. создания не произойдёт).

:: Определяем значение переменной на случай отсутствия ответа пользователя на вопрос.
Set "Adding_user_to_group=No"

::@Echo.
::@Echo Будете добавлять в указанную выше группу пользователя? Если да, то введите ( Y ) или ( y ) и нажмите клавишу ввода. Если нет, то нажмите клавишу ввода без ввода данных или введите что хотите. :
@Echo.
Set /P Adding_user_to_group="Будете добавлять в указанную выше группу пользователя? Если да, то введите ( Y ) или ( y ) и нажмите клавишу ввода. Если нет, то нажмите клавишу ввода без ввода данных или введите что хотите.: "

If /I Not "%Adding_user_to_group%"=="Y" Goto Skip_Adding_user_to_group

:We_continue_adding_users_to_group

@Echo.
@Echo Введите имя существующего пользователя для добавления в указанную выше группу и нажмите клавишу ввода.
@Echo Используем именно имя пользователя, т.е. то, что является логином и выдаётся при запуске "%%UserName%%"
@Echo в командной строке (если пользователь вошёл в систему или командная строка запущена от его имени), 
@Echo а не полное имя пользователя (то, что может отображаться в окне приветствия ОС).
@Echo.
Set /P UserName_to_add="Введите имя существующего пользователя для добавления в указанную выше группу и нажмите клавишу ввода: "

:: Делаем пользователя членом нашей группы. Тут можно сразу добавить несколько пользователей.
:: Используем именно имя пользователя, т.е. то, что является логином и выдаётся при запуске "%UserName%"
:: в командной строке (если пользователь вошёл в систему или командная строка запущена от его имени), 
:: а не полное имя пользователя (то, что может отображаться в окне приветствия ОС).
"%N_e_t%" LocalGroup "%Group_name%" "%UserName_to_add%" /Add

:: Определяем значение переменной на случай отсутствия ответа пользователя на вопрос.
Set "Adding_user_to_group=No"

::@Echo.
::@Echo Будете ещё добавлять в указанную выше группу пользователя? Если да, то введите ( Y ) или ( y ) и нажмите клавишу ввода. Если нет, то нажмите клавишу ввода без ввода данных или введите что хотите.:
@Echo.
Set /P Adding_user_to_group="Будете ещё добавлять в указанную выше группу пользователя? Если да, то введите ( Y ) или ( y ) и нажмите клавишу ввода. Если нет, то нажмите клавишу ввода без ввода данных или введите что хотите.: "

If /I "%Adding_user_to_group%"=="Y" Goto We_continue_adding_users_to_group

:Skip_Adding_user_to_group

:: Для подстраховки, на случай, если права уже были изменены сбрасываем их.
:: 
:: Делаем владельцем группу "Администраторы" для этой директории и всего её содержимого.
:: /A - Делает владельцем группу администраторов вместо текущего пользователя.
:: /R - рекурсия: программа будет обрабатывать файлы в указанном каталоге и всех его подкаталогах.
:: /D <ответ> - Ответ по умолчанию, когда текущий пользователь не имеет разрешения "Содержимое папки"
::              на каталог. Это случается при работе с подкаталогами в рекурсивном режиме (/R).
::              Ответы: "Y" (владение) или "N" (пропустить).
"%Take_Own%" /F "%Full_Dir_name%" /A /R /D Y
:: 
:: Сбрасываем права.
:: /T - операция выполняется для всех соответствующих файлов и каталогов, 
::      расположенных в указанных в имени каталогах.
:: /C - выполнение операции продолжается при любых файловых ошибках. 
::      Сообщения об ошибках по-прежнему выводятся на экран.
"%ICACLS_%" "%Full_Dir_name%" /reset /T /C

:: Копируем файл "update-config.json" из директории, 
:: в которой расположен данный CMD-файл в директорию назначения.
Copy /V /Y "%F_P_t_S%\update-config.json" "%Full_Dir_name%\update-config.json"

:: Тут сначала прописываю владельца, а потом группу. И вот почему. Администратор, от имени которого будет
:: запущен данный CMD-файл так же может быть включён в прописываемую группу и тогда после настройки прав
:: владельца уже не поменяешь. А в такой последовательности смена прав пройдёт удачно.
:: 
:: Настройка безопасности файла.
"%ICACLS_%" "%Full_Dir_name%\update-config.json" /setowner "*S-1-5-18" /C
"%ICACLS_%" "%Full_Dir_name%\update-config.json" /remove:g "*S-1-5-32-545" /remove:d "%Group_name%" /deny "%Group_name%":(WD,AD,DE,WDAC,WO) /grant:r "*S-1-5-18":(F) "*S-1-5-32-544":(F) "*S-1-5-32-545":(RX) /inheritance:r /C
:: Настройка безопасности директории.
"%ICACLS_%" "%Full_Dir_name%" /setowner "*S-1-5-18" /C
"%ICACLS_%" "%Full_Dir_name%" /remove:d "%Group_name%" /deny "%Group_name%":(DC,DE,WDAC,WO) /C

@Echo.
@Echo.
@Echo Показ участников указанной выше группы пользователей.
"%N_e_t%" LocalGroup "%Group_name%"

@Echo.
@Echo Работа командного файла закончена.
@Echo.

@Echo Далее, для подстраховки, две статичных паузы.
Pause
Pause

:: 0000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000
:: 0000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000
:: 0000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000
:: 0000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000

