@Echo Off
::@Echo On

:: Выставляем кодовую страницу "Кириллица OEM 866".
CHCP 866

@Echo.
@Echo Назначение данного CMD-файла.
@Echo.
@Echo Сброс прав для директории FireFox'а "%ProgramData%\Mozilla-????????-????-????-????-????????????\
@Echo updates\????????????????" и всего её содержимого.
@Echo.
@Echo.

:: Включение расширенной обработки команд (EnableExtensions)
:: и отложенного раскрытия переменных среды (EnableDelayedExpansion)
SetLocal EnableExtensions
:: EnableDelayedExpansion буду включать по необходимости,
:: т.к. иначе восклицательные знаки в значениях переменных пропадают.
:: Включение - SetLocal EnableDelayedExpansion .
:: Выключение - EndLocal или SetLocal DisableDelayedExpansion .

:: Для нормального отображения русских слов при редактировании файла и его работе,
:: кодировка должна быть Кириллица OEM 866.
:: 

::@Echo.
::@Echo Раскладка клавиатуры должна быть английской!
::@Echo.
::@Echo Keyboard layout has to be English!
::@Echo.

@Echo.
@Echo ФАЙЛ НУЖНО ЗАПУСКАТЬ С ПРАВАМИ АДМИНИСТРАТОРА!
@Echo.

@Echo.
@Echo На момент запуска данного CMD-файла браузер FireFox 
@Echo уже должен быть установлен в ОС и хотя бы один раз запущен!
@Echo.

@Echo.
@Echo Последовательность действий.
@Echo.
@Echo 01. Смена владельца на группу "Администраторы" для директории FireFox'а
@Echo "%ProgramData%\Mozilla-????????-????-????-????-????????????\updates\????????????????" и 
@Echo всего её содержимого.
@Echo 02. Сброс прав для указанной выше директории и всего её содержимого. После этого права у 
@Echo указанной директориии и всего её содержимого будут такие же, как у её родительской директории.

@Echo.
@Echo Далее, для подстраховки, будет две статичных паузы. 
@Echo Если хотите прервать выполнение данного CMD-файла
@Echo закройте консольное окно или нажмите комбинацию кнопок 
@Echo "Ctrl" + "C" и согласитесь на завершение
@Echo выполнения пакетного файла.
@Echo.

Pause
Pause

:: Этот код должен быть в самом начале, т.к. может понадобиться до определения других переменных.
:: Объяснение использования переменной вместо набора знаков.
:: Есть у меня подозрение, что вот такой код "Set "File_7z=%~dp0%Folder_7-Zip%\7za.exe"" иногда 
:: почему-то не работает. Поэтому, вместо использования набора знаков "%~dp0" далее в коде буду 
:: использовать переменную. Скорее всего, во всех других случаях это не нужно, но раз уж создаём 
:: переменную, имеет смысл использовать её везде.
:: 
:: Получаем полный путь до папки, в которой находится данный CMD-файл. Нужно учитывать, 
:: что в конце обязательно будет слэш. Далее, мы его удалим. 
:: F_P_t_S - Full path to script.
::Set "F_P_t_S=%~dp0"
:: 
:: Если последним символом в пути будет слэш, то удаляем его. Кстати, например, ICACLS.exe 
:: отказывается работать с папкой в конце пути которой слэш.
::If /I "%F_P_t_S:~-1%"=="\" Set "F_P_t_S=%F_P_t_S:~0,-1%"
:: 
:: На случай катастрофических ошибок в коде выбираем текущей директорией ту,
:: в которой находится данный CMD-файл. Для того, чтобы не наломать дров в папке, 
:: назначенной текущей, по умолчанию.
::CD /D "%F_P_t_S%"


:: Переменные для укорачивания строки кода (т.к. я предпочитаю указывать полные пути).
:: 
:: Системная программа для восстанавления владельца директории или файла.
Set "Take_Own=%SystemRoot%\System32\TakeOwn.exe"
:: Системная программа для настройки прав директорий и файлов.
Set "ICACLS_=%SystemRoot%\System32\ICACLS.exe"


:: Определяем нужный нам путь.
:: 
:: На сколько я смог понять имена этих дирректорий не меняются, хоть и выглядят они как случайно 
:: сгенерированные. Если они изменятся, придётся здесь их корректировать. Полный путь будет вида 
:: "C:\ProgramData\Mozilla-????????-????-????-????-????????????\updates\????????????????".
:: 
:: В реестре есть такая запись, но, можно ли ей верить в долгосрочной перспективе, это вопрос.
:: [HKEY_LOCAL_MACHINE\SOFTWARE\Mozilla\Firefox\TaskBarIDs]
:: [HKEY_LOCAL_MACHINE\SOFTWARE\WOW6432Node\Mozilla\Firefox\TaskBarIDs]
:: "C:\\Program Files\\Mozilla Firefox"="308046B0AF4A39CB"
:: REG_SZ
:: Совпадает с директорией - 
:: "C:\ProgramData\Mozilla-1de4eec8-1241-4177-a864-e594e8d1fb38\updates\308046B0AF4A39CB"
:: 
Set "Mozilla_Dir_name=Mozilla-1de4eec8-1241-4177-a864-e594e8d1fb38"
Set "FF_Dir_name=308046B0AF4A39CB"
Set "Full_Dir_name=%ProgramData%\%Mozilla_Dir_name%\updates\%FF_Dir_name%"


:: На случай, если напортачим с настройками безопасности NTFS, есть возможность вернуть всё обратно. 
:: Для этого сначала принудительно делаем владельцем всего содержимого директории 
:: "%ProgramData%\Mozilla-????????-????-????-????-????????????\updates\????????????????" группу 
:: "Администраторы" при помощи системной программы "TakeOwn.exe", а потом сбрасываем права для этой же 
:: директории и всего её содержимого при помощи системной программы "ICACLS.exe".
:: 
:: Делаем владельцем группу "Администраторы" для этой директории и всего её содержимого.
:: /A - Делает владельцем группу администраторов вместо текущего пользователя.
:: /R - рекурсия: программа будет обрабатывать файлы в указанном каталоге и всех его подкаталогах.
:: /D <ответ> - Ответ по умолчанию, когда текущий пользователь не имеет разрешения "Содержимое папки"
::              на каталог. Это случается при работе с подкаталогами в рекурсивном режиме (/R).
::              Ответы: "Y" (владение) или "N" (пропустить).
"%Take_Own%" /F "%Full_Dir_name%" /A /R /D Y
:: 
:: Сбрасываем права.
:: /T - операция выполняется для всех соответствующих файлов и каталогов, 
::      расположенных в указанных в имени каталогах.
:: /C - выполнение операции продолжается при любых файловых ошибках. 
::      Сообщения об ошибках по-прежнему выводятся на экран.
"%ICACLS_%" "%Full_Dir_name%" /reset /T /C

@Echo.
@Echo Работа командного файла закончена.
@Echo.

@Echo Далее, для подстраховки, две статичных паузы.
Pause
Pause

